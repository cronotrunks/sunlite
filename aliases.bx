#
#    This file is part of Sunlite for BitchX.
#
#    Sunlite for BitchX is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    (at your option) any later version.
#
#    Foobar is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with Sunlite for BitchX; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#
################################################################################
#
#Hemen ditugu ezkenik skriptaren funtzioak ( aliasak, dena askoz errazagoa izan 
#dadin eta BitchX-arentzako funtzionalidade berriak eransteko).

#Aurrenekoz ban skriptba daukagu, honek autoopeatu egiten gaitu ez bagara 
#operadore kanalen oraindik, skript honek potrozorria deopeatu egiten du baneatu
# baino lehen,nick eta maskara+ip  baneatzen ditu eta mezuarekin kikeatzen du, 
#geroxeago berriro deopeatu egiten gaitu lehenagotik ez bagenuen operik. Script
#hau core & eta kanuedak diseinatu dute.Hemen ematen dizuegu honen kodea X).
#
#Honetaz aparte gure skript bindeatuarentzako ban menu bat dugu.
#Honen tekla konbinazioa Alt-b da :) ¡ ez zarete kexatuko! XDD
alias tban {
  @ pringao = [$0]
  @ mensaje = [$1-]
  if ( [$Sonido] == 1 ) { exec bplay ~/.BitchX/sunlite/sonidos/kick.wav 2> /dev/null }
  if ( [$pringao] != "" ) {
    userhost $pringao -cmd {
      @ ident = [$3]
      @ ip    = [$4] }
    if (![$P] && [$C]) {
      opme
      on -mode 'CHaN $C +o $N' {
        mode $C -o+bb $pringao $pringao*!*@* *!$ident@$ip
        kick $C $pringao $mensaje
	deop $C $N } 
    } {
      mode $C -o+bb $pringao $pringao*!*@* *!$ident@$ip
      kick $C $pringao $mensaje }
 } {
   mode $C -o+bb $pringao $pringao*!*@* *!$ident@$ip
   kick $C $pringao $mensaje } }

alias tban_input {
  @ Ban = ([$Ban] +1)
  exec echo "@ Ban = $Ban" > ~/.BitchX/sunlite/.bans
  input "$cparse("%RNick a banear: ")" {
    if ( [$0] != "" ) {
      if (rmatch($0 $onchannel($C)) >= 1) {
        @ pringao = [$0]
        input "$cparse("%RMensaje de kickeo: ")" {
          if ( [$0] != "" ) {
            tban $pringao $0- <nº $Ban>
          } {
            tban $pringao A casita... <nº $Ban> } } } } } }
if ( [$hotkey] == [ctrl] ) {
  bind ^b parse_command tban_input
} elsif ( [$hotkey] == [alt] ) {
  bind META1-b parse_command tban_input }

# Hau da core & kanuedaren skript orijinala.
#
## Bitarteko funtzioa *ez ikutu*
##
#alias alaputakalle {
#  mode $C +bbb $0*!*@* *!$1@* *!*@$2
#  kick $C $0 $3-
#  ignore $0 all
#}
#
## Operrentzako etekina - /arpeo nick arrazoia
## OP ematen digu, kikeatu, baneatu (nick+ ident + ip)
## eta potroak ikutzen ari zaizkizun edozein ezikusi egiten du XDDD.
## Geroxeago OP kentzen digu ( coresitoren keriak XDDD )
##
#alias arpeo {
# @ kapullo_nick = [$0]
# @ kick_msg     = [$1-]
#
# userhost $kapullo_nick -cmd {
#   @ kapullo_ident = [$3]
#   @ kapullo_ip    = [$4]
# }
#
# if (![$P] && [$C]) {
#  opme
#  on -mode 'CHaN $C +o $N' {
#   alaputakalle $kapullo_nick $kapullo_ident $kapullo_ip $kick_msg
#   deop $C $N
#   on -mode - 'CHaN $C +o $N'
#  }
# } {
#  alaputakalle $kapullo_nick $kapullo_ident $kapullo_ip $kick_msg
# }
#}

# Passwordarra erantsi gabe nick-a  aldatzeko modu eroso bat XD 
# 
#
alias nick {
#  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1 ) {
#    identifica $0 
#  } {
    identifica $0 } 
#}

# Hipanoko bot-ekin errazago ibiltzeko bi alias dira hauek. Ideia hau ere core &
# kanuedarena da. Nik kodea apur bat ikutu dut multiserbidore bot-ak erabiltzeko
# 
alias infoN {
  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
    m NiCK info $0
  } {
    m NickServ info $0 } }

alias infoCh {
  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
    m CHaN info $0
  } {
    m ChanServ info $0 } }

alias memoS {
  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
    m MeMO send $0 $1-
  } {
    m MemoServ send $0 $1- } }

alias memoR {
  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
    m MeMO read $0
  } {
    m MemoServ read $0 } }

alias memoD {
  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
    m MeMO del $0
  } {
    m MemoServ del $0 } }

alias memoL {
  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
    m MeMO list
  } {
    m MemoServ list } }

# Eta hauek nigatik eginak dira ;)
#
alias addlevel {
  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
    m CHaN access $C add $0 $1
  } {
    m ChanServ access $C add $0 $1 } }

alias dellevel {
  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
    m CHaN access $C del $0
  } {
    m ChanServ access $C del $0 } }

alias founder {
  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
    m CHaN identify $C $0-
  } {
    m ChanServ identify $C $0- } }

alias nickreg {
  if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
    m NiCK register $0
  } {
    m NickServ register $0 } }

# Gehien atsegin zaizkigun kanaleetan sartzeko alias bat definitzen dugu. 
# 
#
alias autojoin {
    @ Joined = 1
    sleep 3 
    for ( @ i = 1, i < numwords($Canales_Aj), @ i++) { j $word($i $Canales_Aj) } 
    usleep .5
    clear
    j $word(0 $Canales_Aj) }
if ( [$hotkey] == [ctrl] ) {
  bind ^a parse_command autojoin
} elsif ( [$hotkey] == [alt] ) {
  bind META1-a parse_command autojoin }

# Hau BitchX-arentzako beste etekin bat da.Gure lehioa garbitzen digu eta Topika 
# erakusten digu eta ondoren zein jende dagoen kanalean erakutsi XD (Hot-key ->
# Alt + l).
#
alias limpia {
  clear
  if ( [$C] ) {
    topic $C
    names $C } }
if ( [$hotkey] == [ctrl] ) {
  bind ^l parse_command limpia
} elsif ( [$hotkey] == [alt] ) {
  bind META1-l parse_command limpia }

# Away bezala jartzen gara eta izendatzen gaituenenetzako away notifikazioa 
# aktibatzen dugu ( noticen bitartez ).
#
alias away {
  if ( [$Away] != 1 ) {
    if ( [$0] != "" ) {
      @ Away_msg = [$0-]
    } {
      @ Away_msg = [No toi...] }
    @ Away = 1 
    //away -all $Away_msg   
  } {
    //away 
    @ Away = 0 } }

# Hemen kalkulagailu bezala erabiltzeko etekin bat dugu.
#
alias calculadora {
  input "$cparse("%RInserte operación: ")" {
    if ( [$0] != "" ) {
      exec echo "El resultado de la operación $0 es"
      exec echo "scale=3\;$0" | bc } } }

# Erregistratuta gauden kanaleetan arroba erraz lortzeko era erraza bat. 
# (Hot-key -> Alt + o).
#
alias opme {
  if ( mid(0 1 $winnam($winnum())) == [#] ) {
    if ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1) {
      m CHaN op $C $N 
    } {
      m ChanServ op $C $N } } }
if ( [$hotkey] == [ctrl] ) {
  bind ^o parse_command opme
} elsif ( [$hotkey] == [alt] ) {
  bind META1-o parse_command opme }
  
# Aurkitzen garen kanalen deopatu egiten gara (Hot-key -> Alt +d).
#
alias deopme {
  if ( mid(0 1 $winnam($winnum())) == [#] ) {
    deop $C $N } }
if ( [$hotkey] == [ctrl] ) {
  bind ^d parse_command deopme
} elsif ( [$hotkey] == [alt] ) {
  bind META1-d parse_command deopme }
  
# +v bezala ipintzen gara eta @ kentzen kanaleko operadoreak bagara
# (Hot-key -> Alt +  v).
#
alias voiceme {
  if ( mid(0 1 $winnam($winnum())) == [#] ) {
    voice $N } }
if ( [$hotkey] == [ctrl] ) {
  bind ^v parse_command voiceme
} elsif ( [$hotkey] == [alt] ) {
  bind META1-v parse_command voiceme }
    
# +v kentzen dugu  aurkitzen garen kanalean.
#
alias devoiceme {
  if ( mid(0 1 $winnam($winnum())) == [#] ) {
    devoice $N } }

# Kanalean modu +r jartzen dugu ( flood eraso eta joins & part-entzako).
#
alias moderre {
  if ( mid(0 1 $winnam($winnum())) == [#] ) {
    mode $C +R } }
if ( [$hotkey] == [ctrl] ) {
  bind ^r parse_command moderre
} elsif ( [$hotkey] == [alt] ) {
  bind META1-r parse_command moderre }

#Kanaletik ateratzen gara eta kanal honetan erabiltzen ari ginen lehioa  
# garbitu egiten dugu.
alias part {
  if ( ( winnum() == 1 ) || ( winnum() == 2 ) ) {
    window goto $Principal }
  if ( [$0] != "" ) {
    //part $C $0
  } {
    //part $C $Msg_quit }
  if ( winnum() != 1 ) { 
    window kill 
  } {
    window name Cierrame log off
    clear }
  if ( [$Sonido] == 1 ) { exec bplay ~/.BitchX/sunlite/sonidos/cierraw.wav 2> /dev/null }
  if ( [$partir] == 1 ) {
    window hide_others
    usleep .5
    window swap $Principal
    usleep .5
    window show $Pos2
    window show $Pos1
    recolocar } }
alias p part

# Gaudezen kanaletik sartu eta atera egiten gara.
# (Hot-Key -> Alt + j).
alias rejoin {
  if ( ( winnum() == 1 ) || ( winnum() == 2 ) ) {
    window goto $Principal } 
  @ Canal = [$C]
  //part $C }
if ( [$hotkey] == [alt] ) {
  bind META1-j parse_command rejoin
} elsif ( [$hotkey] == [ctrl] ) {
  bind ^g parse_command rejoin }

on ^timer "%:**" {
  @ Flood = 0
  if ( [$Canal] != "" ) {
    window swap $Canal
    //join $Canal
    @ Canal = "" } }

# Artxibu batera pantallan ateratzen ari dena kopiaten dugu.Framebufferra
# ipinita eduki beharko duzu eta /dev/fb0 irakurtzeko permisua jarrita eduki
# beharko duzu (Hot-Key -> Alt + s). Snapshot-a ikusteko  'bzip2 -d 
# BitchX.jpg.bz2; cp BitchX.jpg /dev/fb0; bzip2 BitchX.jpg
#
alias snapshot {
  @ snap = ([$snap] +1)
  exec cp /dev/fb0 BitchX$snap\.fb
  usleep .5
  exec bzip2 BitchX$snap\.fb }
if ( [$hotkey] == [ctrl] ) {
  bind ^s parse_command snapshot
} elsif ( [$hotkey] == [alt] ) {
  bind META1-s parse_command snapshot }
    
# Lehio berri bat sortzen dugu.
# WNS -> Servidore berri batentzako lehio berri bat.
# WNC -> Lehio berri bat gaudezen serbidorearentzako.
# Query -> Pribatuarenzako lehio berri bat gaudezen serbidorearentzat
# Querys -> Pribatuarenzako lehio berri ba serbidore berriarentzako.
#
alias WNS { 
  if ( ( ( winnum() == 1 ) || ( winnum() == 2 ) ) && ( [$partir] == 1 ) ) {
    window goto $Principal }
  window new server $0 hide swap last name Cierrame double on
  if ( [$partir] == 1 ) recolocar }
alias joins wns
alias server wns

alias WNC {
  if ( ( ( winnum() == 1 ) || ( winnum() == 2 ) ) && ( [$partir] == 1 ) ) {
    window goto $Principal }
  if ( mid(1 1 $0) == [#] ) {
    @ knal = [$0]
  } {
    @ knal = [#$0] }
  if ( winnum($knal) != [-1] ) {
      window swap $knal
  } elsif ( winnam($winnum()) == [Cierrame] ) {
    clear
    //join $knal
    window name $knal logfile ~/.BitchX/sunlite/logs/\#$mid(1 40 $knal)\.$servername($winserv()).log log on
  } {
    window new hide swap last name $knal channel $knal double on logfile ~/.BitchX/sunlite/logs/\#$mid(1 40 $knal)\.$servername($winserv()).log log on 
    if ( [$partir] == 1 ) recolocar } } 
alias join wnc
alias j wnc

alias query {
  @ lineas = 0
  if ( ( ( winnum() == 1 ) || ( winnum() == 2 ) ) && ( [$partir] == 1 ) ) {
    window goto $Principal }
  if ( ( [$0] == [hide] ) && ( winnum($1) == [-1] ) ) {
    @ knal = [$C]
    if ( winnam($winnum()) != [Cierrame] ) { 
      window new query $1 name $1 hide 
    } { 
      //query $1
      clear
      window name $1 }
    if ( [$partir] == 1 ) recolocar 
    @ $1 = 1
    if ( fexist(~/.BitchX/sunlite/logs/$1.log) != -1 ) {
      @ fileid = [$open(~/.BitchX/sunlite/logs/$1.log R)]
      while ( ! ( eof($fileid) == 1 ) ) {
        @ temp = [$read($fileid)]
        @ lineas = ([$lineas] + 1]) }
      if ( close($fileid) == -1 ) xecho -win status $cparse("$Blanco¡¡¡ ERROR !!!$Verde cerrando fichero")
      @ fileid = [$open(~/.BitchX/sunlite/logs/$1.log R)]
      while ( ! ( eof($fileid) == 1 ) ) {
        @ lineas = ([$lineas] - 1])
        @ temp = [$read($fileid)]
        if ( [$lineas] <= 30 ) xecho -win $winnum($1) $cparse("$temp") 
	if ( [$lineas] == 30 ) {
	  echo
	  exec echo "Última sesión abierta `cat /home/trunks/.BitchX/sunlite/logs/trunks.log | grep \"IRC log started\" | tail -n 1 | gawk '{ print $4 \" \" $6 \" \" $5 \" \" $8 \" a las \" $7}'`" } }
      if ( close($fileid) == -1 ) xecho -win status $cparse("$Blanco¡¡¡ ERRROR !!!$Verde cerrando fichero") }
    window logfile ~/.BitchX/sunlite/logs/$1.log log on
  } elsif ( ( [$0] != "" ) && ( [$0] != [hide] ) && ( winnum($0) == [-1] ) ) {
    @ knal = [$0]
    if ( winnam($winnum()) != [Cierrame] ) {
      window new query $0 name $0 hide swap last 
    } {
      //query $0 
      clear
      window name $0 }
    if ( [$partir] == 1 ) recolocar
    if ( [$Sonido] == 1 ) { exec bplay ~/.BitchX/sunlite/sonidos/abrew.wav 2> /dev/null }
    @ $0 = 1
    if ( fexist(~/.BitchX/sunlite/logs/$0.log) != -1 ) {
      @ fileid = [$open(~/.BitchX/sunlite/logs/$0.log R)]
      while ( ! ( eof($fileid) == 1 ) ) {
        @ temp = [$read($fileid)]
        @ lineas = ([$lineas] + 1]) }
      if ( close($fileid) == -1 ) xecho -win status $cparse("$Blanco¡¡¡ ERROR !!!$Verde cerrando fichero")
      @ fileid = [$open(~/.BitchX/sunlite/logs/$0.log R)]
      while ( ! ( eof($fileid) == 1 ) ) {
        @ lineas = ([$lineas] - 1])
        @ temp = [$read($fileid)]
        if ( [$lineas] <= 30 ) xecho -win $winnum($0) $cparse("$temp") 
	if ( [$lineas] == 30 ) {
	  echo
	  exec echo "Última sesión abierta `cat /home/trunks/.BitchX/sunlite/logs/trunks.log | grep \"IRC log started\" | tail -n 1 | gawk '{ print $4 \" \" $6 \" \" $5 \" \" $8 \" a las \" $7}'`" } }
      if ( close($fileid) == -1 ) excho -win status $cparse("$Blanco¡¡¡ ERROR !!! cerrando fichero") }
    window logfile ~/.BitchX/sunlite/logs/$0.log log on
  } elsif ( ( winnum($0) != [-1] ) && ( [$0] != [] ) ) { 
    window swap $0 
  } {
    //query
    @ $winnam($winnum()) = 0
    if ( winnum() != 1 ) {
      window kill
    } {
      window name Cierrame log off
      clear }
    if ( [$Sonido] == 1 ) { exec bplay ~/.BitchX/sunlite/sonidos/cierraw.wav 2> /dev/null }
    if ( [$partir] == 1 ) {
      window hide_others
      usleep .5
      window swap $Principal
      usleep .5
      window show $Pos2
      window show $Pos1
      recolocar } } }
alias q query
if ( [$hotkey] == [ctrl] ) {
  bind ^z parse_command query
} elsif ( [$hotkey] == [alt] ) {
  bind META1-z parse_command query }
    
alias querys {
  if ( ( ( winnum() == 1 ) || ( winnum() == 2 ) ) && ( [$partir] == 1 ) ) {
    window goto $Principal }
  @ knal = [$0]
  window new server $0 hide swap last name $1 query $1 logfile ~/.BitchX/sunlite/logs/$1.log log on
  if ( [$partir] == 1 ) recolocar
  if ( [$Sonido] == 1 ) { exec bplay ~/.BitchX/sunlite/sonidos/abrew.wav 2> /dev/null } }

#Bind honek azken lehiora joateko balio digu. 
if ( [$hotkey] == [ctrl] ) {
  bind ^u SWAP_LAST_WINDOW
} elsif ( [$hotkey] == [alt] ) {
  bind META1-u SWAP_LAST_WINDOW }
    
# Kanalen ban-ak egon ez gero garbitu egiten ditugu.
#
alias fban { 
  if (![$P] && [$C]) {
    opme
    on -mode 'CHaN $C +o $N' {
      mode $C -bbbb $N!*@* *!$my_ident@* *!*@$my_ip *!$my_ident@$my_ip } 
  } {
    mode $C -bbbb $N!*@* *!$my_ident@* *!*@$my_ip *!$my_ident@$my_ip } }

# Konferenzti modua ;)
#
alias Conferencia {
  if ( [$Conferencia] == 1 ) {
    @ Conferencia = 0
    @ Conf_canal = ""
  } {
    @ Conferencia = 1
    @ Conf_canal = [$C] } }
if ( [$hotkey] == [ctrl] ) {
  bind ^c parse_command Conferencia
} elsif ( [$hotkey] == [alt] ) {
  bind META1-c parse_command Conferencia }
    
# BitchX-etik sms bidaltzeko balio digu.
# 
#
alias sms {
  if (( [$0] == "" ) || ( [$1] == "" )) {
    xecho -win status $cparse("Modo de uso: /sms gateway nombre texto")
    xecho -win status $cparse("Gateways soportados por esms: navegalia y movistar")
  } {
    exec echo $2- | esms -s $0 $1 } }

# Funtzio honek izendatu gaituzten kanalean sarbide azkarra lortzeko balioko 
# digu.
alias cambio_ventana {
  if ( ( ( winnum() == 1 ) || ( winnum() == 2 ) ) && ( [$Partir] == 1 ) ) {
    window goto $Principal }
  if ( [$Nick_en_canal] != "" && [$Nick_en_canal] != [$C] ) {
      window swap $Nick_en_canal
      @ Nick_en_canal = "" } }
if ( [$hotkey] == [ctrl] ) {
  bind ^x parse_command cambio_ventana
} elsif ( [$hotkey] == [alt] ) {
  bind META1-x parse_command cambio_ventana }

# /shell <password> ipinita BX bigarren planuan edukiko dugu beste gauza bat  
# egiten dugun bitartean.Inork guk idatzitakoa irakur ez dezan ordenagailua 
# aske utzi behar dugunerako ere balio du ;) BitchX-era bueltatzeko 'scr-bx -p'
# ipini beharko dugu eta ondoren passworra.

alias shell {
  if ( [$0] != [] ) detach $0 }

# Marrazkitxoak atsegin dituztenei eginiko skript-a da hau, skript honi alias
# batzuk ipini dizkiogu ascii figuratxoak egin ditzazun ( cowsay eta figlet )
# istalaturik eduki beharko dituzu.
#
alias cowsay {
  switch ( $0 ) {
    ( normal ) { exec -o cowsay $1- }
    ( risa ) { exec -o cowsay -y $1- }
    ( tux ) { exec -o cowsay -f tux $1- }
    ( burla ) { exec -o cowsay -d $1- }
    ( susto ) { exec -o cowsay -w $1- }
    ( * ) {
      xecho -win status Modo de uso: /cowsay {normal|burla|tux|susto} texto } } }

alias figlet {
  switch ( $0 ) {
    ( normal ) { exec -o figlet $1- }
    ( mini ) { exec -o figlet -f mini $1- }
    ( banner ) { exec -o figlet -f banner $1- }
    ( italic ) { exec -o figlet -f slant $1- }
    ( script ) { exec -o figlet -f script $1- }
    ( block ) { exec -o figlet -f block $1- }
    ( bubble ) { exec -o figlet -f bubble $1- }
    ( digital ) { exec -o figlet -f digital $1- }
    ( * ) {
      xecho -win status Modo de uso: /figlet {normal|banner|mini|italic|script|block|bubble} texto } } }
	  
# mpg123 erabiliko dugu mp3 entzuteko gure BitchX-ean ;) Komanduak hauetxek 
# dira: 
#  
#   /mp3 list - Mp3 edukina erakusten digu ( horretarako Mp3dir bat sortu 
#   beharko dugu).
#			
#   /mp3 play RefNum|file	- Guk ipinitako abestia berregiten du.
#   /mp3 stop		- Berregite prozesua bukatzen du.
#   /mp3 dir ruta	- Abestiak beste toki batetik hartzeko balio du.
#			
#
alias mp3 {
  switch ( $0 ) {
    ( list ) {
      exec ls -1 -F $Mp3dir | gawk '{print "[" , NR, "]", $0}'
      xecho -win status $cparse("$Blanco\Para reproducir: $AzulF\/mp3 play Refnum|Nombre_archivo") }
    ( play ) {
      @ RefNum = [$1]
      if ( [$1] == "" )  {
        xecho -win status $cparse("$Blanco\Indica un titulo para tocar, ¿no? :)")
      } elsif ( isdigit($1) ) {
        exec mpg123 "$Mp3dir/`ls -1 $Mp3dir/ | head -n $RefNum | tail -n 1`" 2> /dev/null
        if ( [$Notif_mp3] == 1 ) {
	    exec -o  echo "Mmm... Toi escuchando `ls -1 $Mp3dir/ | head -n $RefNum | tail -n 1`" }
      } elsif ( [$1] == [\*] ) { 
        exec mpg123 $Mp3dir/\* 2> /dev/null
      } elsif ( [$1] == [\*/\*] ) {
        exec mpg123 $Mp3dir/\*/\* 2> /dev/null
      } {
        exec mpg123 "$Mp3dir/$RefNum" 2> /dev/null
        if ( [$Notif_mp3] == 1 ) {
	  exec -o  echo "Mmm... Toi escuchando `ls -1 $Mp3dir/ | grep $RefNum`" } } }
    ( stop ) {
      exec killall mpg123 2> /dev/null }
    ( dir ) {
      if ( [$1-] != "" ) {
        @ Mp3dir = [$1-]
      } {
        xecho -win status $cparse("$Blanco\El directorio de mp3's es [$Mp3dir]. Si quieres cambiarlo teclea: $AzulF\/mp3 dir path") } }
    ( * ) {
      xecho -win status $cparse("$Blanco\Modo de uso: $AzulF\/mp3 {list|play|stop|dir}") } } }

# Gure BitchX-ean zedeak entzuteko cdcd erabiliko dugu, erabiltzeko komanduak
# hauxek dira:
#   /cd list           - Zedean dauden pistak erakusten dizkigu
#   /cd play RefNum    - Nahi dugun pista berregiten digu.
#   /cd stop           - Berregite prozesua bukatzen du.
#   /cd refresh        - Pisten lista berresortzen du.
#
alias cd {
  switch ( $0 ) {
    ( list ) { 
      exec cdcd tracks }
    ( play ) {
      @ RefNum = [$1]
      if ( isdigit($1) ) {
        exec cdcd play $RefNum
        @ RefNum = ([$RefNum] + 6)
        if ( [$Notif_mp3] == 1 ) {
          exec -o echo "Mmm... Toi escuchando `cdcd tracks | grep \"Album artist\" | cut -c 17-`: `cdcd tracks | head -n $RefNum | tail -n 1 | cut -c 21-`" }       
      } elsif ( [$RefNum] == [\*] ) {
        exec cdcd rndplay 
      } {
        xecho -win status $cparse("$Blanco\Modo de uso: $Azulf\/cd {list|play RefNum|stop|refresh}") } }
    ( stop ) { 
      exec cdcd stop }
    ( refresh ) { 
      exec cdcd refresh }
    ( * ) { 
      xecho -win status $cparse("$Blanco\Modo de uso: $Azulf\/cd {list|play|stop|refresh}") } } }

# Version berria ipintzen diogu gure BitchX-ari, hau da gure skriptaren aipamena# ipintzen diogu. 
# 
#
alias version {
  //version
  echo $cparse("$RojoF-$Blanco:$RojoF-") $J }

# Gure BitchX-arentzako kolore berriak.
#
alias color {
  if ( ( [$0] == [si] ) || ( [$0] == [sí] ) || ( [$VerdeF] == [] ) ) {
    if ( [$theme] == [bass] ) {
      @ VerdeF = [%g]
      @ Blanco = [%c]
      @ Amarillo = [%B]
      @ Magenta = [%Y]
      @ Violeta = [%c]
      @ Verde = [%g]
      @ RojoF = [%r]
      @ Rojo = [%r]
      @ AzulF = [%B]
      @ Gris = [%w]
      @ Naranja = [%b]
      @ CyanF = [%C]
      @ Cyan = [%c]
      set mircs on
    } {
      @ VerdeF = [%G]
      @ Verde = [%g]
      @ RojoF = [%R]
      @ Rojo = [%r]
      @ AzulF = [%B]
      @ Blanco = [%W]
      @ Gris = [%w]
      @ Amarillo = [%Y]
      @ Naranja = [%y]
      @ Magenta = [%M]
      @ Violeta = [%m]
      @ CyanF = [%C]
      @ Cyan = [%c]
      set mircs on }
  } {
    @ VerdeF = []
    @ Verde = []
    @ RojoF = []
    @ Rojo = []
    @ AzulF = []
    @ Blanco = []
    @ Gris = []
    @ Amarillo = []
    @ Naranja = []
    @ Magenta = []
    @ Violeta = []
    @ CyanF = []
    @ Cyan = [] 
    set mircs off } }

alias sonido {
  if ( ( [$0] == [si] ) || ( [$0] == [sí]) || ( [$Sonido] == 0 ) || ( [$0] == [on] ) ) {
    set beep on
    @ Sonido = 1
  } {
    set beep off
    @ Sonido = 0 } }

# L00PB4cK-ek aginiko kodea eta Trunks-ek berriztatua.
#
# Nick identifikazioa password enkriptatuak dituen artxibu batetik.
#
alias identifica { 
  if ( [$0] != "" ) {
    @ NNick = [$0]
  } {
    @ NNick = [$N] }
  if ($fexist(~/.BitchX/sunlite/passwd.bx) != -1) {
    @ fileid = [$open(~/.BitchX/sunlite/passwd.bx R)]
    @ findid = 0
    while ( ( eof($fileid) == 0) && ( $findid != 1) ) {
      @ temp = [$read($fileid)]
      @ temp1 = [$decode($temp)]
      @ nickid = [$midw(0 1 $temp1)]
      @ passid = [$midw(1 1 $temp1)]
      @ net = [$midw(2 1 $temp1)]
      if (( [$NNick] == [$nickid] ) && ( [$findid] == 0 )) {
        if ( ( match(hispano $reverse($mid(4 7 $reverse($net)))) >= 1) && ( match(hispano $reverse($mid(4 7 $reverse($servername($winserv()))))) >= 1 )) {
          msg nick@deep.space identify $passid
          quote nick $NNick:$passid
          @ findid = 1
        } elsif ( ( match(openprojects $reverse($mid(4 12 $reverse($net)))) >= 1 ) && ( match(openprojects $reverse($mid(4 12 $reverse($servername($winserv()))))) >= 1 )) {
          //nick $NNick
          msg nickserv identify $passid
          @ findid = 1
        } elsif ( ( match(dal $reverse($mid(4 3 $reverse($net)))) >= 1 ) && ( match(dal $reverse($mid(4 3 $reverse($servername($winserv()))))) >= 1 )) {
          //nick $NNick
          msg nickserv@services.dal.net identify $passid
          @ findid = 1 } } }
    if ( close($fileid) == -1) {
      xecho -win status $cparse("$Blanco!!!$RojoF ERROR $Blanco!!!$Verde cerrando base de datos") }
    if ([$findid] == 0) {
      input "$cparse("%RIntroduzca clave para $NNick: ")" {
        if ( [$0] != "" ) {
          @ passid = [$0]
        @ fileid = [$open(~/.BitchX/sunlite/passwd.bx R)]
        while ( ( eof($fileid) == 0) && ( $findid != 1 ) ) {
          @ temp = [$read($fileid)]
          @ temp1 = [$decode($temp)]
          @ nickid = [$midw(0 1 $temp1)]
          @ net = [$midw(2 1 $temp1)]
          if ([$NNick] == [$nickid]) {
            if ( ( servername($winserv()) == match($net $reverse($mid(4 7 $reverse($servername($winserv()))))) || servername($winserv()) == match($net $reverse($mid(4 3 $reverse($servername($winserv()))))) || servername($winserv()) == match($net $reverse($mid(4 12 $reverse($servername($winserv()))))))) {
              @ find = 1 } } }
        if ( close($fileid) == 1) {
          xecho -win status $cparse("$Verde\Error Cerrando el fichero") }
        if ( [$find] == 1 ) {
           xecho -win status $cparse("$Verde\El nick $1 ya está incluído en la base de datos bajo la red $3")
        } {
          @ fileid = [$open(~/.BitchX/sunlite/passwd.bx W)]
          @ temp2 = [$NNick] ## [ ] ## [$passid] ## [ ] ## [$servername($winserv())]
          @ encoded = [$encode($temp2)]
          if ( write($fileid $encoded) == -1) {
            xecho -win status $cparse("$Verde\Escribiendo en el fichero")
          } {
            xecho -win status $cparse("$Verde\Datos escritos correctamente en el fichero") 
            identifica $NNick }
          if ( close($fileid) == 1) {
            xecho -win status $cparse("$Verde\Error cerrando fichero") } } } } } } }

# Gure nick-a aldatzeko away bezala jartzen garenerako.|Poldark|-en ideia  
# orijinala. 
#  
alias awayon {
  nick $nick_awayOn
  deopme
  away $0- }

alias awayoff {
  nick $nick_awayOff
  if ( ! [$P] && [$C] ) {
    on -nickname '$N' opme }
  away
  if ( [$0] != "" ) {
    me $0- } }

# Irc-tik ateratzen garenerako skriptaren quit pertsonalizatua.
#
alias quit {
  if ( [$0] == "" ) {
    //quit $Msg_quit
  } {
    //quit $0- } }

# Alias honekin tty batetan aurkitzen garenean eta X-etara edozer gauz bidali
# nahi dugunerako oso aproposa, adibidez url bat nabegagailu batetar bidali nahi
# dugunean
##  - Eskerrak Poldark-eri bere ideiarengatik--
#
alias termX {
  exec echo '$0-' > /dev/`ps u | grep pts | head -n1 | gawk '{print $7}'` }

# Kanpotar kanaleetan txateatzeko alias bat erantsi diogu, alias honek ezagutzen
# ez ditugun hitzak itzuliko dizkigu
# nola erabili: /traducir [inglés|ingles|english|español|spanish] palabra ( Sin argumento
#Inglesetik erdara itzultzen du XD 
#
alias traducir {
  if ( [$0] == "" ) {
    xecho -win status $cparse("$Blanco\Modo de uso: $AzulF/traducir [ingles|inglés|english|español|spanish] palabra \( Por defecto traducirá al castellano una palabra en inglés \)")
  } elsif ( ( [$0] == [inglés] ) || ( [$0] == [ingles] ) || ( [$0] == [english] ) ) { 
    exec i2e.sh -re $1
  } elsif ( ( [$0] == [español] ) || ( [$0] == [spanish] ) ) {
    exec i2e.sh -e $1
  } {
    exec i2e.sh -e $0 } }
alias translate traducir

# Alias honekin lehio guztiak bere ordenean jarriko dizkizu skriptak edozein 
# mometutan hutsegiten badizu 
# 
#
alias recolocar {
  switch ( $Pos1 ) {
    ( 11 ) {
      switch ( $Pos2 ) {
        ( 12 ) {
          window goto 1
          window size 5
          window goto 2
          window size 7
          window goto 3 }
        ( 1 ) {
          window goto 3
          window size 7
          window goto 1
          window size 5
          window goto 2 } } }
    ( 12 ) {
      switch ( $Pos2 ) {
        ( 11 ) {
          window goto 1
          window size 7
          window goto 2
          window size 5
          window goto 3 }
        ( 1 ) {
          window goto 3
          window size 5
          window goto 1
          window size 7
          window goto 2 } } }
    ( 1 ) {
      switch ( $Pos2 ) {
        ( 11 ) {
          window goto 2
          window size 5
          window goto 3
          window size 7
          window goto 1 }
        ( 12 ) {
          window goto 2
          window size 7
          window goto 3
          window size 5
          window goto 1 } } } } }

# Hemen dijoazkizue pare bat hot-key: Lehiotatik mugitzeko funtzio ebakia 
# erabiltzen badugu eta beste pare bat ezker eskubira mugitzeko ;)  
#  
#
if ( [$hotkey] == [alt] ) {
  bind META1-q NEXT_WINDOW
  bind META1-w SWAP_PREVIOUS_WINDOW
  bind META1-e SWAP_NEXT_WINDOW
  bind META1-n notify
} elsif ( [$hotkey] == [ctrl] ) {
  bind ^q NEXT_WINDOW
  bind ^w SWAP_PREVIOUS_WINDOW
  bind ^e SWAP_NEXT_WINDOW 
  bind ^n notify }

bind META32-~ beginning_of_line
bind META33-~ end_of_line
bind META1-, beginning_of_line
bind META1-. end_of_line

# Copyright © 2002 Javier Carranza
